# 基于Hive实现的每月最高温度的三天

有一个文件，其中记录了某一天的某个时间的温度，格式为“yyyy-MM-dd HH:mm:ss\ttemperature”，如“2008-01-01 10:20:30  36”。现需要统计每个月温度最高的三天及它们的温度。

```text
2004-07-14 10:19:02	10
2002-01-05 18:17:59	34
2000-05-10 01:26:51	7
2004-02-21 03:21:22	13
2004-11-09 04:12:12	9
2004-06-11 07:18:38	3
2001-07-17 03:31:20	11
```

先在hive上创建天气数据存放的表，语句如下：

```sql
CREATE TABLE weather_data (
    record_time timestamp,
    temperature int
)
ROW FORMAT 
DELIMITED FIELDS TERMINATED BY '\t'; 
```

然后把hadoop上的天气文件导入到刚刚创建的weather_data表。

```text
hive> load data inpath '/user/elim/data/weather/input' Into table weather_data;
Loading data to table test.weather_data
OK
Time taken: 0.635 seconds
```

> 天气数据存放在hadoop的/user/elim/data/weather/input目录下。导入的数据文件在hadoop上时会直接把hadoop上的文件移动到hive表对应的hadoop文件路径下，而不是拷贝。

由于原始数据中存在一天记录了多个温度的情况，通过如下语句可以查看按照每天对每天的温度做个排名。

```sql
 select date_format(record_time,'yyyy-MM-dd'),temperature,row_number() over (partition by date_format(record_time,'yyyy-MM-dd') order by temperature desc) rank from weather_data;
```

> row_number()是获取行号，后面的over表示按照年月日分组并按照温度从高到低排序。

上面的SQL执行后可以看到如下内容：

```text
2000-01-16	13	1
2000-01-18	9	1
2000-02-02	24	1
2000-03-11	11	1
2000-04-03	2	1
2000-04-12	10	1
2000-04-22	0	1
...
```

然后我们通过rank=1即可取到每天的最高温度。

```sql
select t.* from (
    select date_format(record_time,'yyyy-MM-dd'),temperature,row_number() over (partition by date_format(record_time,'yyyy-MM-dd') order by temperature desc) rank from weather_data
) t
where rank=1;
```

先创建一张结果表存储统计出来的每天的最高气温。

```sql
CREATE TABLE weather_result1 (
    record_date date,
    record_temperature int
);
```

通过如下语句可以把每天的最高气温的数据插入到表weather_result1。

```sql
from (
    select date_format(record_time,'yyyy-MM-dd') record_date,temperature,row_number() over (partition by date_format(record_time,'yyyy-MM-dd') order by temperature desc) rank from weather_data
) t
insert into weather_result1
select t.record_date,t.temperature where rank=1;
```

同样的，如下SQL可以按月分组，然后按温度从高到低排序。

select record_date,record_temperature,row_number() over (partition by date_format(record_date,'yyyy-MM') order by record_temperature desc) rank from weather_result1;

我们可以再建如下这样一张表存储最后的结果。

```sql
CREATE TABLE weather_result2 (
    record_date date,
    record_temperature int
);
```

通过如下语句可以统计出每月温度最高的三天，并把它们写入到weather_result2表中。

```sql
from (select record_date,record_temperature,row_number() over (partition by date_format(record_date,'yyyy-MM') order by record_temperature desc) rank from weather_result1) t
insert into weather_result2
select t.record_date,t.record_temperature where rank <=3;
```

> 温度最高的三天中可能有几天的温度是一样的，使用row_number()时会把筛选出来的分组的数据连续的排号，从1开始。rank()会把相同的温度作为同一个排名，然后会跳号，比如113336这样排列，中间跳过了第2和第5。dense_rank()和rank()一样会把相同的温度给予相同的排名，但它不会跳号，比如112223这样排列。所以如果对最高的理解有不同，可以使用rank()或dense_rank()替换上面的row_number()。


